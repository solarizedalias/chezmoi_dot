
# vim: set ft=zsh:

# ==========================================================
# cdump
# Mon Jul 27 17:32:29 2020
# AUTHOR: solarizedalias
# As compdump ignores autoload funcs stored in *.zwc...
# ==========================================================

emulate -LR zsh
setopt warn_create_global typeset_silent no_short_loops no_auto_pushd
setopt extended_glob      glob_dots      rc_quotes      hist_subst_pattern
setopt NO_clobber         NO_append_create

local VERBOSE="${@[(I)(-v|--verbose)]}"
local SIDEEFFECT="${@[(I)-s|--side-effect]}"
local NODUMP="${@[(I)(-n|--no-dump)]}"

local -a fpath_save=( ${(@)fpath} )
trap '(( ${#fpath} < ${#fpath_save} )) && fpath=( ${(@)fpath_save} ); return 1' INT

fpath=( ${(@)^fpath%.zwc}(#qND-/) )

local -a autoloads
autoloads=(
  ${(M@)${(ok)functions[(R)builtin autoload -XU]}:#_[^_]*}
)


if [[ ${#fpath} -ne ${#fpath_save} ]]; then
  print -u2 "Expected ${#fpath_save} elements, but got ${#fpath}."
  (( VERBOSE )) && {
    print -u2 -- '${(@)fpath_save|fpath}'
    print -u2 -- "${(@)fpath_save:|fpath}"
    print -u2 -- ''
    print -u2 -- '${(@)fpath:|fpath_save}'
    print -u2 -- "${(@)fpath:|fpath_save}"
  }

  fpath=( ${(@)fpath_save} )
  return 1
else
    (( NODUMP )) || compdump
    (( SIDEEFFECT )) || fpath=(${(@)fpath_save})
fi

