#compdef tig

# vim: set ft=zsh:

# ==========================================================
# _tig
# Thu Jul 23 16:14:56 2020
# based on tig version 2.5.1
# AUTHOR: solarizedalias
# The missing pieces for the implementation of _git as the
# completions for tig.
# ==========================================================

setopt localoptions NO_shwordsplit
setopt warn_create_global typeset_silent no_short_loops no_auto_pushd
setopt extended_glob      glob_dots      rc_quotes      hist_subst_pattern

local context curcontext=${curcontext} state line expl state_descr ret=1
declare -A opt_args
local -a tig_commands=(
  'log:Start up in log view, displaying git-log output.'
  'show:Open diff view using the given git-show options.'
  'reflog:Start up in reflog view.'
  'blame:Show given file annotated by commits. Takes zero or more git-blame options. Optionally limited from given revision.'
  'grep:Open the grep view. Supports the same options as git-grep.'
  'refs:Start up in refs view.'
  'stash:Start up in stash view.'
)

# Depends on _git from the zsh repository.
(( ${+functions[_git-log]} )) || {
  autoload -Uz _git && _git || : 2>/dev/null
}
local ign
(( CURRENT > 2 )) && ign='!'

_arguments \
  '(: - *)'{-v,--version}'[Show version and exit]' \
  '(-h --help)'{-h,--help}'[Show help message and exit]' \
  ${ign}'-C[Start in <path>]:path:{_path_files -/}' \
  '1:: :{_describe "commands" tig_commands}' \
  '*: :{_git-log}' && ret=0

