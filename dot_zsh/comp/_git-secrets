#compdef git-secrets

# vim: set ft=zsh:

# ==========================================================
# _git_secrets
# Wed Oct 21 11:05:58 2020
# based on git-secrets version 1.3.0
# AUTHOR: solarizedalias
#
# ==========================================================

setopt localoptions NO_shwordsplit
setopt warn_create_global typeset_silent no_short_loops no_auto_pushd
setopt extended_glob      glob_dots      rc_quotes      hist_subst_pattern

local context curcontext=${curcontext} state line expl state_descr ret=1
local -A opt_args val_args

_arguments \
  - scan \
    '--scan[scan file for prohibited patterns]:files:{ _path_files }' \
    '(-r --recursive)'{-r,--recursive}'[scan directories recursively]' \
    '--cached[scan searches blobs registerd in the index file]' \
    '--no-index[search files in the current directory that  is not managed by git]' \
    '--untracked[scan also untracked files]' \
  - history \
    '--scan-history[scan repo for prohibited patterns]' \
  - install \
    '--install[install git hooks for repository or template directory]:directory:{ _path_files -/ }' \
    '(-f --force)'{-f,--force}'[overwrite hookds if the hook already exists]' \
  - list \
    '--list[list secret patterns]' \
    '--global[use the global git config]' \
  - add \
    '--add[add a prohibited or allowed pattern]' \
    '(-a --allowed)'{-a,--allowed}'[add an allowed pattern instead of prohibited]' \
    '(-l --literal)'{-l,--literal}'[treat patterns as literal strings]' \
    '--global[use the global git config]' \
  - provider \
    '--add-provider[add a secret provider that when called outputs secret patterns on new lines]:command:{
      _command_names
    }:*::args:{
      emulate zsh -o NO_warn_create_global -o extended_glob -c "_dispatch _ ${${(s.:.)opt_args[provider---add-provider]}[1]}"
    }' \
    '--global[use the global git config]' \
  - awsprovider \
    '--aws-provider[secret provider that outputs credentials found in an ini file]:credential-file:{ _path_file }' \
  - registeraws \
    '--register-aws[add common aws patterns to the git config and scan for ~/.aws.credentials]' \
    '--global[use the global git config]' && ret=0

return ret

