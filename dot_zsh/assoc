
# vim: set ft=zsh:

# ==========================================================
# assoc
# Sat Oct 31 21:22:07 2020
# AUTHOR: solarizedalias
#
# ==========================================================

emulate -LR zsh
setopt warn_create_global typeset_silent no_short_loops no_auto_pushd
setopt extended_glob      glob_dots      rc_quotes      hist_subst_pattern
setopt NO_clobber NO_append_create

local -a opts seps
zparseopts -a opts -D -K -M -E \
  h -help=h \
  v -version=v \
  s:=seps -separator:=s \
  V -visible=V \
  n -no-sort=n # XXX not implemented

local -a reply
local MATCH MBEGIN MEND
[[ ${parameters[$1]} == association* ]] || return 1

local SEP=${seps[2]}
integer VISIBLE=${opts[(I)(-V|--visible)]}

if builtin zformat -a reply ${SEP:- } \
    ${(okP)1//(#m)*/${MATCH//:/\\:}:${${(P)1}[${MATCH}]}}; then
  if (( VISIBLE )); then
    builtin print -rl -- ${(@V)reply}
  else
    builtin print -rl -- ${(@)reply}
  fi
fi

